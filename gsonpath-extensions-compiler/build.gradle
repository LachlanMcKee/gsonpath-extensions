apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.lachlanmckee'
version = '1.0.0'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "net.lachlanmckee:gsonpath-compiler-base:$gsonpath_compiler_version"
    compile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())

    testCompile "junit:junit:$junit_version"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'gsonpath-extensions-compiler'
                    packaging 'jar'
                    description 'A Gson Path extension library that enhances the existing validation'
                    url 'http://lachlanmckee.net/gsonpath/'

                    scm {
                        url 'scm:git@github.com:LachlanMcKee/gsonpath-extensions.git'
                        connection 'scm:git@github.com:LachlanMcKee/gsonpath-extensions.git'
                        developerConnection 'scm:git@github.com:LachlanMcKee/gsonpath-extensions.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'lachlanmckee'
                            name 'Lachlan McKee'
                        }
                    }
                }
            }
        }
    }
}