apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'net.lachlanmckee'
version = '1.1.0'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

buildscript {
    ext.kotlin_version = '1.0.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.5"

    compile 'net.lachlanmckee:gsonpath-compiler-base:1.1.0'

    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.google.auto:auto-common:0.6'
    compile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'gsonpath-extension-android'
                    packaging 'jar'
                    description 'A Gson Path extension library that adds Android support'
                    url 'http://lachlanmckee.net/gsonpath/'

                    scm {
                        url 'scm:git@github.com:LachlanMcKee/gsonpath-extensions-android.git'
                        connection 'scm:git@github.com:LachlanMcKee/gsonpath-extensions-android.git'
                        developerConnection 'scm:git@github.com:LachlanMcKee/gsonpath-extensions-android.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'lachlanmckee'
                            name 'Lachlan McKee'
                        }
                    }
                }
            }
        }
    }
}